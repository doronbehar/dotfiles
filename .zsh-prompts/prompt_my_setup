prompt_my_setup(){
	prompt_opts=(subst percent)
	autoload -Uz vcs_info
	zstyle ':vcs_info:*' disable bzr cdv darcs mtn svk tla
	zstyle ':vcs_info:*' check-for-changes true
	if [ -z $DISPLAY ]; then
		zstyle ':vcs_info:*' formats ${${PIPENV_ACTIVE:+%K\{54\}%F\{blue\}>%F\{white\}PE%K\{cyan\}%F\{54\}>}:-%K\{cyan\}%F\{blue\}>}'%K{cyan}%F{black} î‚  %b%c%u %K{green}%F{cyan}>'
		zstyle ':vcs_info:*' nvcsformats ${${PIPENV_ACTIVE:+%K\{54\}%F\{blue\}>%F\{white\}PE%K\{green\}%F\{54\}>}:-%K\{green\}%F\{blue\}>}
		zstyle ':vcs_info:*' stagedstr '+'
		zstyle ':vcs_info:*' unstagedstr '-'
	else
		zstyle ':vcs_info:*' formats ${${PIPENV_ACTIVE:+%K\{54\}%F\{blue\}î‚°%F\{white\}ðŸŒ´%K\{cyan\}%F\{54\}î‚°}:-%K\{cyan\}%F\{blue\}î‚°}'%K{cyan}%F{black} î‚  %b%c%u %K{green}%F{cyan}î‚°'
		zstyle ':vcs_info:*' nvcsformats ${${PIPENV_ACTIVE:+%K\{54\}%F\{blue\}î‚°%F\{white\}ðŸŒ´%K\{green\}%F\{54\}î‚°}:-%K\{green\}%F\{blue\}î‚°}
		zstyle ':vcs_info:*' stagedstr 'âœš'
		zstyle ':vcs_info:*' unstagedstr 'â—'
	fi
	add-zsh-hook precmd prompt_my_precmd
	add-zsh-hook preexec prompt_my_preexec
	if [ -z $DISPLAY ]; then
		PS1_base='%(!.%K{red}%F{white}%B %n.%K{blue}%F{white}%B %n)${SSH_TTY:+@%M} %b${vcs_info_msg_0_}%K{green}%F{black} %3c %(1j.%K{yellow}%F{green}>%K{yellow}%F{black}%B %j %b%K{default}%F{yellow}>.%K{default}%F{green}>)%b%u%k%f '
		PS2='%K{blue}%F{white} %^ %K{default}%F{blue}> '
		RPS1='%(?..%K{default}%F{red}<%K{red}%F{black} %? )'
	else
		PS1_base='%(!.%K{red}%F{white}%B %n.%K{blue}%F{white}%B %n)${SSH_TTY:+@%M} %b${vcs_info_msg_0_}%K{green}%F{black} %3c %(1j.%K{yellow}%F{green}î‚°%K{yellow}%F{black}%B %j %b%K{default}%F{yellow}î‚°.%K{default}%F{green}î‚°)%b%u%k%f '
		PS2='%K{blue}%F{white} %^ %K{default}%F{blue}î‚° '
		RPS1='%(?..%K{default}%F{red}î‚²%K{red}%F{black} %? )'
	fi
	zle -N zle-line-init
	zle -N zle-keymap-select
	terminfo_down_sc=$terminfo[cud1]$terminfo[cuu1]$terminfo[sc]$terminfo[cud1]
}

prompt_my_precmd () {
	vcs_info
}

# Inspired/shamlessly-taken from: http://stackoverflow.com/questions/3622943/zsh-vi-mode-status-line
function zle-line-init zle-keymap-select {
	PS1_2="${${KEYMAP/vicmd/-- NORMAL --}/(main|viins)/-- INSERT --}"
	PS1="%{$terminfo_down_sc$PS1_2$terminfo[rc]%}$PS1_base"
	zle reset-prompt
}

prompt_my_preexec () {
	print -rn -- $terminfo[el];
}

prompt_my_setup

# vim:ft=zsh
