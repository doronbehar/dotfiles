#compdef nix
# shellcheck disable=all
#
# Why do I override a file that's already distributed by Nix? See:
# https://github.com/NixOS/nix/issues/14625

function _nix() {
  local ifs_bk="$IFS"
  local input=("${(Q)words[@]}")
  IFS=$'\n'
  local res=($(NIX_GET_COMPLETIONS=$((CURRENT - 1)) "$input[@]" 2>/dev/null))
  IFS="$ifs_bk"
  local tpe="${${res[1]}%%>	*}"
  local -a suggestions
  declare -a suggestions
  for suggestion in ${res:1}; do
    suggestions+=("${suggestion%%	*}")
  done
  local -a args
  if [[ "$tpe" == filenames ]]; then
    args+=('-f')
  elif [[ "$tpe" == attrs ]]; then
    args+=('-S' '')
  fi
  if [[ "$tpe" == normal ]] && (( ${#suggestions} == 0 )); then
    # In case there are no suggestions by Nix,
    _files
  else
    compadd -J nix "${args[@]}" -a suggestions
  fi
}

_nix "$@"
